plugins {
    id 'java'
    id 'maven-publish'
    id 'io.qameta.allure' version '2.8.0'
    id 'org.gradle.test-retry' version '1.3.1'
}

group = 'youtrack'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

def allureVersion = "2.13.6"
def junit5Version = "5.7.2"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
    implementation 'org.seleniumhq.selenium:selenium-server:3.141.59'
    implementation 'io.qameta.allure:allure-junit5:2.14.0'
    implementation 'org.apache.commons:commons-lang3:3.8.1'
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation 'io.github.bonigarcia:webdrivermanager:3.7.1'

    implementation 'org.hamcrest:hamcrest:2.2'
    implementation 'ch.qos.logback:logback-classic:1.2.5'

    implementation("io.qameta.allure:allure-java-commons:$allureVersion")
    implementation("org.junit.jupiter:junit-jupiter-api:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-engine:$junit5Version")
    implementation("org.junit.jupiter:junit-jupiter-params:$junit5Version")

    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'
}

allure {
    autoconfigure = true
    aspectjweaver = true
    version = allureVersion

    clean = true

    useJUnit5 {
        version = allureVersion
    }
}

task parallelTest(type: Test) {
    maxParallelForks = 3
    ignoreFailures = true
    useJUnitPlatform {
    }
    systemProperty("junit.jupiter.execution.parallel.enabled", "true")
    systemProperty("junit.jupiter.execution.parallel.config.strategy", "dynamic")
    systemProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    exclude '**/MaximumNumberOfUsersTest.class'
}

task singleThreadTest(type: Test) {
    ignoreFailures = true
    useJUnitPlatform {
    }

    include '**/MaximumNumberOfUsersTest.class'
}

tasks.withType(Test).configureEach {
    doFirst {
        systemProperty('applicationBaseUrl', System.getProperty('applicationBaseUrl') ?: 'http://localhost:8080')
        systemProperty('adminUserLogin', System.getProperty('adminUserLogin') ?: 'admin')
        systemProperty('adminUserPassword', System.getProperty('adminUserPassword') ?: '111')
        retry {
            failOnPassedAfterRetry = true
            maxRetries = 2
        }
    }
}

test.dependsOn parallelTest, singleThreadTest
